plugins {
   id 'net.nemerosa.versioning' version '2.6.1'
   id "nebula.ospackage" version "4.6.0"
   id 'de.fuerstenau.buildconfig' version '1.1.8'
}
apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'io.finn.signald.Main'

compileJava.options.encoding = 'UTF-8'


repositories {
    maven {url "https://raw.github.com/AsamK/maven/master/releases/"}
    maven {url "https://plugins.gradle.org/m2/"}
    mavenCentral()
}

dependencies {
    compile 'com.github.turasa:signal-service-java:2.6.5_unofficial_1'
    compile 'org.bouncycastle:bcprov-jdk15on:1.55'
    compile 'com.kohlschutter.junixsocket:junixsocket-native-common:2.0.4'
    compile 'org.apache.logging.log4j:log4j-api:2.10.0'
    compile 'org.apache.logging.log4j:log4j-core:2.10.0'
    compile 'io.sentry:sentry-log4j2:1.6.4'
}

apply plugin: 'net.nemerosa.versioning'
apply plugin: "nebula.deb"

//version = versioning.info.full
version = "${System.env.VERSION}"
buildConfig {
  version = version
  appName = "${System.env.CI_PROJECT_NAME}"
  packageName = "io.finn.signald"
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': project.mainClassName,
        )
    }
}


task deb(type: Deb) {
    def applicationName = "signald"
    def distInstallPath = "/usr/lib/signald"
    packageName = 'signald'
    version = version
    release = 1
    preInstall file('src/main/resources/io/finn/signald/debian-preinstall.sh')
    requires('libunixsocket-java')
    requires('openjdk-8-jre-headless')

    from("build/install/signald") {
        exclude '**/*.bat'
        into distInstallPath
    }

    from("src/main/resources/io/finn/signald/signald.service") {
      into "/lib/systemd/system"
    }

    link("/usr/bin/${applicationName}", "${distInstallPath}/bin/${applicationName}")

    from('src/main/resources') {
        fileType CONFIG | NOREPLACE
        into 'conf'
    }
}
